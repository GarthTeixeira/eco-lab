package org.example ;

import java_cup.runtime.*;
import java.util.*;

parser code {:
  private HashMap<String,Object> symT = new HashMap<String,Object>();

  public boolean isDeclared(String id) {
    return symT.containsKey(id);
  }

  public boolean isNumber(Object o) {
    return o instanceof Integer || o instanceof Float;
  }

  public void syntax_error(Symbol s){
          this.defineErro(s.left, s.right);
  }

  public void unrecovered_syntax_error(Symbol cur_token){

  }



  public void defineErro(int linha, int coluna, String texto)
  {
              AnalisadorLexico sc = (AnalisadorLexico) this.getScanner();
              sc.defineErro(linha, coluna, texto);
  }

  public void defineErro(int linha, int coluna){
          AnalisadorLexico sc = (AnalisadorLexico) this.getScanner();
          sc.defineErro(linha, coluna);
  }

  public void defineErro(String texto){
          AnalisadorLexico sc = (AnalisadorLexico) this.getScanner();
          sc.defineErro(texto);
  }

:}

terminal PALAVRA_RESERVADA_PROGRAM, IDENTIFICADOR, PALAVRA_RESERVADA_DECLARE, PALAVRA_RESERVADA_BEGIN, PALAVRA_RESERVADA_END,
PALAVRA_RESERVADA_INTEGER, PALAVRA_RESERVADA_DECIMAL, PALAVRA_RESERVADA_IF, PALAVRA_RESERVADA_THEN, PALAVRA_RESERVADA_ELSE,
PALAVRA_RESERVADA_DO, PALAVRA_RESERVADA_WHILE, PALAVRA_RESERVADA_FOR, PALAVRA_RESERVADA_TO, PALAVRA_RESERVADA_READ,
PALAVRA_RESERVADA_WRITE, LEFT_PARENTHESIS, RIGHT_PARENTHESIS, LITERAL, OPERADOR_ASSIGN, SEMICOLON, COLON, OPERADOR_EQUAL,
OPERADOR_GREAT, OPERADOR_GREATEQUAL, OPERADOR_LESS, OPERADOR_LESSEQUAL, OPERADOR_DIFFERENT, OPERADOR_ADD, OPERADOR_MINUS,
PALAVRA_RESERVADA_OR, OPERADOR_MULT, OPERADOR_PART, PALAVRA_RESERVADA_MOD, PALAVRA_RESERVADA_AND, OPERADOR_LEFT_SHIFT,
OPERADOR_RIGHT_SHIFT, OPERADOR_UNSIGNED_RIGHT_SHIFT, OPERADOR_UNSIGNED_LEFT_SHIFT, PALAVRA_RESERVADA_NOT,COMMA, INTERROGATION;

terminal Integer CONSTANT;
terminal Float FLOAT_CONSTANT;

non terminal program, body, decl_list, decl, type, stmt_list, stmt, assign_stmt, if_stmt, condition,
do_while_stmt, stmt_suffix, for_stmt, while_stmt, read_stmt, write_stmt, writable, expression, simple_expr, term,
factor_a, factor, relop, addop, mulop, shiftop, ternary_expr;

non terminal List<String> ident_list;

start with program;

program ::= PALAVRA_RESERVADA_PROGRAM IDENTIFICADOR body | error {:
                parser.defineErro("Erro sintático: verifique o inicio e o fim do programa");
            :};

body ::= PALAVRA_RESERVADA_DECLARE decl_list PALAVRA_RESERVADA_BEGIN stmt_list PALAVRA_RESERVADA_END | error {:
                parser.defineErro("Erro sintático: Corpo do programa invalido");
            :};

decl_list ::= decl SEMICOLON | decl_list decl SEMICOLON | error {:
                parser.defineErro("Erro sintático: Declaracao invalida");
            :};

decl ::= type:type ident_list:id_list
    {:

        for (String id : id_list)
            if (isDeclared(id))
                parser.defineErro( id_listleft,id_listright ,"Erro Semântico: Variavel ja declarada");
            else
                if (type.equals("integer"))
                    symT.put(id, new Integer(0));
                else
                    symT.put(id, new Float(0));
    :};

ident_list ::= IDENTIFICADOR:id {: RESULT = new ArrayList<String>() {{add(id.toString());}};:} | IDENTIFICADOR:id COMMA ident_list:id_list
    {:
        List<String> aux = new ArrayList<String>();
        aux.addAll(id_list);
        aux.add(id.toString());
        RESULT = aux;
    :};

type ::= PALAVRA_RESERVADA_INTEGER:int_word{:RESULT = int_word;:} |
PALAVRA_RESERVADA_DECIMAL:float_word{:RESULT = float_word;:} | error {:
                parser.defineErro("Erro sintático: Tipo invalido");
            :};

stmt_list ::= stmt SEMICOLON | stmt_list stmt SEMICOLON | error {:
                parser.defineErro("Erro sintático: Comando invalido - Sugestão checar ponto e virgula");
            :};

stmt ::= assign_stmt | if_stmt | while_stmt | do_while_stmt| for_stmt | read_stmt | write_stmt | error {:
                parser.defineErro("Erro sintático: Comando invalido");
            :};

assign_stmt ::= IDENTIFICADOR:id OPERADOR_ASSIGN simple_expr:smpl_ex {:
                    if (!isDeclared(id.toString()))
                        parser.defineErro(idleft,idright,"Erro Semântico: Variavel " + id.toString() + " não declarada");
                    else{
                        if (!isNumber(smpl_ex))
                            parser.defineErro(smpl_exleft,smpl_exright,"Erro Semântico: Expressao nao numerica");
                        else if (smpl_ex instanceof Float){
                            if (symT.get(id.toString()) instanceof Integer)
                                parser.defineErro(smpl_exleft,smpl_exright,"Erro Semântico : Atribuicao de decimal a inteiro");
                            else
                                symT.put(id.toString(), (Float)smpl_ex);
                        }
                        else{
                            if (symT.get(id.toString()) instanceof Float)
                                parser.defineErro(smpl_exleft,smpl_exright,"Erro Semântico : Atribuicao de inteiro a decimal");
                            else
                                symT.put(id.toString(), (Integer)smpl_ex);
                        }
                    }
                :}
                 |
                 IDENTIFICADOR:id OPERADOR_ASSIGN ternary_expr:tern_ex{:
                    if (!isNumber(tern_ex))
                        parser.defineErro(tern_exleft,tern_exright,"Erro Semântico: Expressao nao numerica");
                    else if (tern_ex instanceof Float){
                        if (symT.get(id.toString()) instanceof Integer)
                            parser.defineErro(tern_exleft,tern_exright,"Erro Semântico : Atribuicao de decimal a inteiro");
                        else
                            symT.put(id.toString(), (Float)tern_ex);
                    }
                    else{
                        if (symT.get(id.toString()) instanceof Float)
                            parser.defineErro(tern_exleft,tern_exright,"Erro Semântico : Atribuicao de inteiro a decimal");
                        else
                            symT.put(id.toString(), (Integer)tern_ex);
                    }
                 :};

if_stmt ::= PALAVRA_RESERVADA_IF condition PALAVRA_RESERVADA_THEN stmt_list PALAVRA_RESERVADA_END | PALAVRA_RESERVADA_IF
condition PALAVRA_RESERVADA_THEN stmt_list PALAVRA_RESERVADA_ELSE stmt_list PALAVRA_RESERVADA_END;

condition ::= expression:exp
                {:
                    if ( exp instanceof Boolean) RESULT = exp;
                    else parser.defineErro(expleft,expright,"Erro Semântico: Expressao nao booleana");
                :};

do_while_stmt ::= PALAVRA_RESERVADA_DO stmt_list stmt_suffix;

stmt_suffix ::= PALAVRA_RESERVADA_WHILE condition;

for_stmt ::= PALAVRA_RESERVADA_FOR assign_stmt PALAVRA_RESERVADA_TO condition PALAVRA_RESERVADA_DO stmt_list
PALAVRA_RESERVADA_END;

while_stmt ::= PALAVRA_RESERVADA_WHILE condition PALAVRA_RESERVADA_DO stmt_list PALAVRA_RESERVADA_END;

read_stmt ::= PALAVRA_RESERVADA_READ LEFT_PARENTHESIS IDENTIFICADOR:id RIGHT_PARENTHESIS{:
                    if (!isDeclared(id.toString()))
                        parser.defineErro(idleft,idright,"Erro Semântico: Variavel " + id.toString() + " não declarada");
                    else{
                        if (symT.get(id.toString()) instanceof Integer)
                            symT.put(id.toString(), new Integer(0));
                        else
                            symT.put(id.toString(), new Float(0));
                    }
:};

write_stmt ::= PALAVRA_RESERVADA_WRITE LEFT_PARENTHESIS writable:w RIGHT_PARENTHESIS {:System.out.println(w);:} ;

writable ::= expression:ex{:RESULT = ex;:} | LITERAL:l{:RESULT = ((String) l).toString().replaceAll("\"","");:};

expression ::= simple_expr:smpl_ex
                {:RESULT = smpl_ex;:}
               |
               simple_expr:smpl_ex_a relop:relop simple_expr:smpl_ex_b
               {:
               if (!isNumber(smpl_ex_a) || !isNumber(smpl_ex_b))
                    parser.defineErro(smpl_ex_aleft,smpl_ex_bright,"Erro Semântico: Expressão não numerica - Sugestão checar operadores e precedência");
                else {
                    if (relop.equals("="))
                        RESULT = new Boolean(((Integer)smpl_ex_a).intValue() == ((Integer)smpl_ex_b).intValue());
                    else if (relop.equals(">"))
                        RESULT = new Boolean(((Integer)smpl_ex_a).intValue() > ((Integer)smpl_ex_b).intValue());
                    else if (relop.equals(">="))
                        RESULT = new Boolean(((Integer)smpl_ex_a).intValue() >= ((Integer)smpl_ex_b).intValue());
                    else if (relop.equals("<"))
                        RESULT = new Boolean(((Integer)smpl_ex_a).intValue() < ((Integer)smpl_ex_b).intValue());
                    else if (relop.equals("<="))
                        RESULT = new Boolean(((Integer)smpl_ex_a).intValue() <= ((Integer)smpl_ex_b).intValue());
                    else if (relop.equals("<>"))
                        RESULT = new Boolean(((Integer)smpl_ex_a).intValue() != ((Integer)smpl_ex_b).intValue());
                    }
               :}
               | ternary_expr:tern_ex
               {:RESULT = tern_ex;:};

ternary_expr ::= LEFT_PARENTHESIS expression:exp RIGHT_PARENTHESIS INTERROGATION simple_expr:smpl_ex_b COLON simple_expr:smpl_ex_c
                {:
                    if (exp instanceof Boolean)
                        RESULT = ((Boolean)exp).booleanValue() ? smpl_ex_b : smpl_ex_c;
                    else
                        parser.defineErro(expleft,expright,"Erro Semântico : Expressao nao booleana na codincao ternária");
                :};

simple_expr ::= term:t
                {:RESULT = t;:}
                |
                simple_expr:smpl_ex addop:addop term:t
                {:
                    if (addop.equals("or")){
                        if (t instanceof Boolean && smpl_ex instanceof Boolean)
                            RESULT = ((Boolean)t).booleanValue() || ((Boolean)smpl_ex).booleanValue();
                        else
                            parser.defineErro(smpl_exleft,smpl_exright,"Erro Semântico: Operador logico aplicado a um valor nao booleano");
                    } else if(!isNumber(smpl_ex) || !isNumber(t)){
                            parser.defineErro(smpl_exleft,smpl_exright,"Erro Semântico: Operador aritmetico aplicado a um valor nao numerico");
                     } else{
                         if (t instanceof Float && smpl_ex instanceof Float){
                            if (addop.equals("+"))
                                RESULT = ((Float)smpl_ex).floatValue() + ((Float)t).floatValue();
                            else if (addop.equals("-"))
                                RESULT = ((Float)smpl_ex).floatValue() - ((Float)t).floatValue();
                         }else if (t instanceof Integer && smpl_ex instanceof Integer){
                            if (addop.equals("+"))
                                RESULT = ((Integer)smpl_ex).intValue() + ((Integer)t).intValue();
                            else if (addop.equals("-"))
                                RESULT = ((Integer)smpl_ex).intValue() - ((Integer)t).intValue();
                         } else if (smpl_ex instanceof Float && t instanceof Integer){
                            if (addop.equals("+"))
                                RESULT = ((Float)smpl_ex).floatValue() + ((Integer)t).intValue();
                            else if (addop.equals("-"))
                                RESULT = ((Float)smpl_ex).floatValue() - ((Integer)t).intValue();
                         } else if (t instanceof Integer && smpl_ex instanceof Float){
                            if (addop.equals("+"))
                                RESULT = ((Integer)smpl_ex).intValue() + ((Float)t).floatValue();
                            else if (addop.equals("-"))
                                RESULT = ((Integer)smpl_ex).intValue() - ((Float)t).floatValue();
                         }
                     }
                :}
                | error
                {:parser.defineErro("Erro Sintático: Expressão invalida");:};

term ::= factor_a:f_a
             {:RESULT = f_a;:}
             |
             term:t mulop:mulop factor_a:f_a
             {:
                if (mulop.equals("and")){
                    if (t instanceof Boolean && f_a instanceof Boolean)
                        RESULT = ((Boolean)t).booleanValue() && ((Boolean)f_a).booleanValue();
                    else
                        parser.defineErro(tleft,tright,"Erro Semântico: Operador logico aplicado a um valor nao booleano");
                } else if(!isNumber(t) || !isNumber(f_a)){
                    parser.defineErro(tleft,tright,"Erro Semântico: Operador aritmetico aplicado a um valor nao numerico");
                } else if ((t instanceof Float) && (f_a instanceof Float)){
                    if (mulop.equals("*"))
                        RESULT = ((Float)t).floatValue() * ((Float)f_a).floatValue();
                    else if (mulop.equals("/")){
                        if (((Float)f_a).floatValue() == 0.0)
                            parser.defineErro(tleft,tright,"Erro Semântico: Divisao por zero");
                        else RESULT = ((Float)t).floatValue() / ((Float)f_a).floatValue();
                    }
                    else if (mulop.equals("mod"))
                        RESULT = ((Float)t).floatValue() % ((Float)f_a).floatValue();
                } else if ((t instanceof Integer) && (f_a instanceof Integer)){
                    if (mulop.equals("*"))
                        RESULT = ((Integer)t).intValue() * ((Integer)f_a).intValue();
                    else if (mulop.equals("/")){
                        if (((Integer)f_a).intValue() == 0)
                            parser.defineErro(tleft,tright,"Erro Semântico: Divisao por zero");
                        else RESULT = ((Integer)t).intValue() / ((Integer)f_a).intValue();
                    }else if (mulop.equals("mod"))
                        RESULT = ((Integer)t).intValue() % ((Integer)f_a).intValue();
                } else if ((t instanceof Float) && (f_a instanceof Integer)){
                    if (mulop.equals("*"))
                        RESULT = ((Float)t).floatValue() * ((Integer)f_a).intValue();
                    else if (mulop.equals("/")){
                        if (((Integer)f_a).intValue() == 0)
                            parser.defineErro(tleft,tright,"Erro Semântico: Divisao por zero");
                        else RESULT = ((Float)t).floatValue() / ((Integer)f_a).intValue();
                    }
                    else if (mulop.equals("mod"))
                        RESULT = ((Float)t).floatValue() % ((Integer)f_a).intValue();
                } else if ((t instanceof Integer) && (f_a instanceof Float)){
                    if (mulop.equals("*"))
                        RESULT = ((Integer)t).intValue() * ((Float)f_a).floatValue();
                    else if (mulop.equals("/")){
                        if (((Float)f_a).floatValue() == 0.0)
                            parser.defineErro(tleft,tright,"Erro Semântico: Divisao por zero");
                        else RESULT = ((Integer)t).intValue() / ((Float)f_a).floatValue();
                    }
                    else if (mulop.equals("mod"))
                        RESULT = ((Integer)t).intValue() % ((Float)f_a).floatValue();
                }
             else
                parser.defineErro(tleft,tright,"Erro Semântico: Operador aritmetico aplicado a um valor nao numerico");
        :};

factor_a ::= factor:f
            {:RESULT = f;:}
             |
             PALAVRA_RESERVADA_NOT factor:f
             {: if (f instanceof Boolean)
                    RESULT = !((Boolean)f).booleanValue();
                else
                    parser.defineErro(fleft,fright,"Erro Semântico: Operador not aplicado a um valor nao booleano");
             :}
             |
             OPERADOR_MINUS factor:f
             {: if ( f instanceof Integer)
                    RESULT = -((Integer)f).intValue();
                else if (f instanceof Float)
                    RESULT = -((Float)f).floatValue();
                else
                    parser.defineErro(fleft,fright,"Erro Semântico: Operador unario aplicado a um valor nao numerico");
              :};

factor ::=  IDENTIFICADOR:id
            {:
                if (symT.containsKey(id.toString()))
                    RESULT = symT.get(id.toString());
                else
                    parser.defineErro(idleft,idright,"Erro Semântico: Variável " + id.toString() + " não declarada");
            :}
            |
            CONSTANT:c
            {:RESULT = c;:}
            |
            FLOAT_CONSTANT:fc
            {:RESULT = fc;:}
            |
            LEFT_PARENTHESIS expression:e RIGHT_PARENTHESIS
            {:RESULT = e;:};

relop ::= OPERADOR_EQUAL:op_equal{:RESULT = op_equal;:} | OPERADOR_GREAT:op_greater{:RESULT = op_greater;:} |
    OPERADOR_GREATEQUAL:op_greater_equal{:RESULT = op_greater_equal;:} | OPERADOR_LESS:op_less{:RESULT = op_less;:} |
    OPERADOR_LESSEQUAL:op_less_equal{:RESULT = op_less_equal;:} | OPERADOR_DIFFERENT:op_not_equal{:RESULT = op_not_equal;:};

addop ::= OPERADOR_ADD:op_add{:RESULT = op_add;:} | OPERADOR_MINUS:op_minus{:RESULT = op_minus;:} |
    PALAVRA_RESERVADA_OR:op_or{:RESULT = op_or;:};

mulop ::= OPERADOR_MULT:op_mult{:RESULT = op_mult;:} | OPERADOR_PART:op_div{:RESULT = op_div;:} |
    PALAVRA_RESERVADA_MOD:op_mod{:RESULT = op_mod;:} | PALAVRA_RESERVADA_AND:op_and{:RESULT = op_and;:};

shiftop ::= OPERADOR_LEFT_SHIFT | OPERADOR_RIGHT_SHIFT | OPERADOR_UNSIGNED_RIGHT_SHIFT | OPERADOR_UNSIGNED_LEFT_SHIFT;